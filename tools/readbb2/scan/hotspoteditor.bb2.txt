; ascii translation of /Users/simon/Google Drive/amiga/w/fdrive/junk/bum6/usercode/fabio/HotSpotEditor.bb2
; Hot Spot Editor
; Starting Date: 2 Jan 1994

; * simon was here, wo sorry some calls to reqtools library
;	 which we haven't got a standard library number for, I
;	 will try and implement this for bum7

	#ANY=0
	#CHIP=2
	#FAST=4
	#CLEAR=$10000
	#LARGEST=$20000
	#TOTAL=$80000
	rtStatus.b=0
	ref.w=100

NEWTYPE .rtFileRequester
	rtReqPos.l
	rtLeftOffset.w
	rtTopOffset.w
	rtFlags.l
	rtPrivate0.b[4]
	rtDir.l
	rtMatchPat.l
	rtDefaultFont.l
	rtWaitPointer.l
	rtLockWindow.l
	rtShareIdcmp.l
	rtReserved1.b[8]
End NEWTYPE

	DEFTYPE.b
	flag.l=0
	xb=0:yb=0:depth=0:xr=0:yr=0:xm=0:ym=0
	ver$="1.0"

.setup
	WBStartup:FindScreen 0
	LoadFont 0,"topaz.font",8

	MenuTitle 0,0," Actions "
	MenuChecked 0
	MenuItem 0,0,0,0,"Load Shapes		 ","L"
	MenuItem 0,0,0,1,"Save Shapes		 ","S"
	MenuItem 0,0,0,2,"Load Palette		","P"
	MenuItem 0,0,0,3,"Animation			 ","A"
	MenuItem 0,0,0,4,"Reconfig				","R"
	MenuItem 0,0,0,5,"Quit						","Q"
	SelectMode 1,0:SetString 2,1
	TextGadget 0,136,52,0,1,"LO-RES|HI-RES"
	TextGadget 0,196,52,0,2,"NOLACE| LACE "
	SetString 1,2
	StringGadget 0,320,52,0,0,2,16	:; Screen Depth
	SetString 2,1
	TextGadget 0,180,85,0,20," GO! "
	ButtonId 0,0,"5"
	Window 0,120,50,380,100,$100A,"Hot Spot Editor V"+ver$+" - SETUP",0,1,0
	WindowFont 0
	WLocate 28,14
	NPrint "Welcome To Hot Spot Editor By Fabio Soft."
	NPrint "		 Please Configure Your Own Screen Mode."
	NPrint "			And The Depth Of Your Working Screen."
	WLocate 28,44:Print "Screen Mode:"
	WLocate 268,44:Print "Depth:"
	Repeat
		ev.l=WaitEvent
		If ev=$200 Then End
		If ev=$40
			gad=GadgetHit
			If gad=20
				width.w=320:height.w=256:more.b=50
				If Disable(0,1)=2 Then flag+$8000:width=640
				If Disable(0,2)=2 Then flag+4:height=512:more.b=100
				depth=Val(GadgetPens(0,0))
				If depth>8 Then depth=5
				Pop Repeat: Goto mkscreen
			EndIf
			FlushEvents
		EndIf
	Forever

.getname
	*f.rtFileRequester=##[$ED01] (0,0)
	reqstring$=String$(" ",30)
	rtStatus.b=##[$ED05] (*f,&reqstring$,&rtMessage$,0)
	##[$ED02] *f
	If rtStatus<>0
		For t=Len(reqstring$) To 1 Step -1
			x$=Mid$(reqstring$,t,1)
			If x$<>" " AND x$<>Chr$(0) AND x$<>Chr$(10)
				reqstring$=Mid$(reqstring$,1,t)
				Pop For:Goto rtskp1
			EndIf
		Next
		rtskp1:
		path$=Peeks$(*f\rtDir,100)
		For t=Len(path$) To 1 Step -1
			x$=Mid$(path$,t,1)
			If x$<>" " AND x$<>Chr$(0) AND x$<>Chr$(10)
				path$=Mid$(path$,1,t)
				Pop For:Goto rtskp2
			EndIf
		Next
		rtskp2:
		If Right$(path$,1)<>":" OR Right$(path$,1)<>"/"
			path$+"/"
		EndIf
		fn$=path$+reqstring$
	EndIf
	Return

.mkscreen
	Screen 0,0,0,width,height,depth,flag,"Hot Spot Editor V"+ver$,1,2
	Free GadgetJam 0
	VWait 3
	TextGadget 0,40,12,0,1," ^ "
	TextGadget 0,8,24,0,2," < "
	TextGadget 0,72,24,0,3," > "
	TextGadget 0,40,36,0,4," v "
	TextGadget 0,40,24,0,5," x "
	TextGadget 0,104,12,0,6,"Top	Left"
	TextGadget 0,8,12,0,7," <^"
	TextGadget 0,72,12,0,8," ^>"
	TextGadget 0,8,36,0,9," <v"
	TextGadget 0,72,36,0,10," v>"
	TextGadget 0,104,24,0,11,"Same	Hot"
	TextGadget 0,104,36,0,12," << "
	TextGadget 0,144,36,0,13," >> "
	TextGadget 0,194,12,0,14,"-^^-"
	TextGadget 0,234,12,0,15,"-vv-"
	StringGadget 0,250,24,0,16,5,40
	Window 0,0,12,width,height-12,$0400+$0800,"",1,2
	ScreensBitMap 0,0
	Window 1,0,height-50,width,50,$1002," Controls ",1,2,0
	WLocate 190,16
	Print "Shape: "
	WLocate 190,28
	Print "X:		 Y:		"
	SetMenu 0
	skp:
	Repeat
		ev.l=WaitEvent
		If ev=8 AND EventWindow=0 AND maxshapes<>0
			hotspotx=-(SMouseX-width/2)
			hotspoty=-(SMouseY-80)
			Handle currentshape,hotspotx,hotspoty
			Gosub blitall
		EndIf
		If ev=256
			Select MenuHit
				Case 0
					Select ItemHit
						Case 0
							rtMessage$="Get An Anim Brush File!"
							Gosub getname
							Window 3,0,0,width,50,$1000,"Sys Request",1,2
							Print "How Many Shapes ":maxshapes=Edit(4)
							Free Window 3
							If maxshapes>Maximum MidHandle-1 Then maxshape=Maximum MidHandle-1
							Use Window 0
							LoadShapes 0,fn$
							currentshape=0
							hotspotx=0
							hotspoty=0
							Gosub blitall
						Case 1
							rtMessage$="Insert Save File Name!"
							Gosub getname
							SaveShapes 0,maxshapes,fn$
						Case 2
							rtMessage$="Get A Palette File!"
							Gosub getname
							LoadPalette 0,fn$
							Use SetCycle 0
						Case 3
							Gosub animation
						Case 4
							Pop Repeat
							Free Window 0:Free Window 1
							Free GadgetJam 0
							Goto setup
						Case 5
							End
					End Select
			End Select
		EndIf
		If ev=64
			Select GadgetHit
				Case 1
					hotspoty+1
				Case 2
					hotspotx+1
				Case 3
					hotspotx-1
				Case 4
					hotspoty-1
				Case 5
					hotspotx=ShapeWidth(currentshape)/2
					hotspoty=ShapeHeight(currentshape)/2
				Case 6
					hotspotx=0
					hotspoty=0
				Case 7
					hotspoty+1
					hotspotx+1
				Case 8
					hotspotx-1
					hotspoty+1
				Case 9
					hotspotx+1
					hotspoty-1
				Case 10
					hotspotx-1
					hotspoty-1
				Case 16
					currentshape=Val(GadgetPens(0,16))
					If currentshape>maxshapes
						currentshape=maxshapes
						ButtonId 0,16,Str$(currentshape)
						Redraw 1,16
					EndIf
					Gosub blitall
			End Select
			If GadgetHit<12
				Handle currentshape,hotspotx,hotspoty
				Gosub blitall
			EndIf
		EndIf
		If ev=32
			Select GadgetHit
				Case 12
					currentshape-1
					If currentshape<0 Then currentshape=0
				Case 13
					currentshape+1
					If currentshape>maxshapes Then currentshape=maxshapes
				Case 14
					Gosub lineup
				Case 15
					Gosub linedown
				End Select
				Gosub blitall
		EndIf
	Forever
	End

.linedown
	Use Window 0
	Repeat
		WLine 0,ref,width,ref,0
		ref+1
		If ref>height Then ref=height
		WLine 0,ref,width,ref,1
		VWait
	Until Joyb(0)=0
	Return

.lineup
	Use Window 0
	Repeat
		WLine 0,ref,width,ref,0
		ref-1
		If ref<12 Then ref=12
		WLine 0,ref,width,ref,1
		VWait
	Until Joyb(0)=0
	Return

.blitall
	If maxshapes=0 Then Return
	Use Window 1
	ButtonId 0,16,Str$(currentshape):Redraw 1,16
	WLocate 204,28
	Print Centre$(Str$(hotspotx),4)
	WLocate 260,28
	Print Centre$(Str$(hotspoty),4)
	Use BitMap 0
	Use Window 0
	InnerCls
	Point width/2,80,1
	Line 0,ref,width,ref,1
	ClipBlit currentshape,width/2,80
	Use Window 1
	VWait
	Return

.animation
;	If maxshapes<1 Then Return
	Screen 1,0,0,width,height,depth,flag,"Animation Screen",1,2
	ScreensBitMap 1,1
	Use SetCycle 0
	StringGadget 1,46,14,0,1,5,40
	StringGadget 1,120,14,0,2,5,40
	TextGadget 1,210,14,0,3,"	 Loop	 | Ping-Pong |	Reverse	"
	StringGadget 1,54,28,0,4,3,32
	TextGadget 1,120,28,0,5,"Start"
	TextGadget 1,210,28,0,6,"	Get Back "
	ButtonId 1,1,Str$(currentshape)
	ButtonId 1,2,Str$(currentshape)
	ButtonId 1,4,"5"
	Window 3,0,0,width,height,$0400+$0800,"",1,2
	Window 2,0,height-50,width,50,0,"Animation Controls",1,2,1
	WLocate 1,6:Print "From:"
	WLocate 92,6:Print "To:"
	WLocate 166,6:Print "Mode:"
	WLocate 1,20:Print "Speed:"
	Repeat
		ev=WaitEvent
		If ev=64
			Select GadgetHit
				Case 5
					Gosub animate
				Case 6
					Free Window 3
					Free Window 2
					Free GadgetJam 1
					Free BitMap 1
					Free Screen 1
					Pop Repeat:Goto skp
			End Select
		EndIf
	Forever
	Return

.animate
	mde.b=Disable(1,3)
	frm.w=Val(GadgetPens(1,1))
	dest.w=Val(GadgetPens(1,2))
	speed.w=Val(GadgetPens(1,4))
	Use Window 3
	aaa=ShapeHeight(frm)
	Repeat
		If mde<>3
			For t=frm To dest
				VWait speed
				InnerCls
				Blit t,width/2,aaa
			Next
			If mde=2
				For t=dest-1 To frm+1 Step -1
					VWait speed
					InnerCls
					Blit t,width/2,aaa
				Next
			EndIf
		Else
			For t=dest To frm Step -1
				VWait speed
				InnerCls
				Blit t,width/2,aaa
			Next
		EndIf
	Until Joyb(0)<>0
	Return
