; ascii translation of /Users/simon/Google Drive/amiga/w/lha/bbold/cricket/bup/ui.bb,/Users/simon/Google Drive/amiga/w/lha/bbold/cricket/bup2/ui.bb
;
; cricket scoring program
;

; (C)1994 Acid Software

NEWTYPE .batter
	ballsfaced.w
	runs.w
	extras.w
	fours.w
	sixes.w
	minutes.w
	status.w
	bowler.w
	fielder.w
End NEWTYPE

NEWTYPE .bowler
	overs.w					 ;bowling
	maidens.w
	runsallowed.w
	wickets.w
	extras.w
	balls.w
	noballs.w
	byes.w
	legbyes.w
	wides.w
End NEWTYPE

NEWTYPE .team
	teamname$
	teamabbrev$
	name$[12]
	r.w:g:b				 ;56
End NEWTYPE

NEWTYPE .side
	team.team
	score.w
	ba.w[12]				;batting order
	bo.w[12]				;bowling order
	bat.batter[12]
	bowl.bowler[12]
End NEWTYPE

NEWTYPE .ball
	over.w
	ball.w
	batter.w
	bowler.w
	runs.w
	extras.w
	status.w
	catcher.w
End NEWTYPE

NEWTYPE .entry
	;
	message$			 ;displayed in view window
	id.w					 ;0=empty 1=toss 2=innings 3=over 4=ball 5=out 6=newbatter
	;
	batting.w			;1
	;
	batter.w[2]		;2
	;
	bowler.w			 ;3
	whichend.w
	;
	facing.w			 ;4
	runs.w
	extras.w
	shot.w
	wx.w
	wy.w
	;
	reason.w			 ;5
	fielder.w
	;
	newbatter.w		;6
	;
End NEWTYPE

Dim team.side(1)
Dim entry.entry(1023)
Dim bat.w(2)

Dim ends$(2)
Dim shot$(6)
Dim out$(6)

ends$(0)="NEAR END":ends$(1)="FAR END"
shot$(0)="SWINGS":shot$(1)="NOBALL":shot$(2)="WIDE":shot$(3)="BYE"
shot$(4)="LEGBYE":shot$(5)="FOUR!":shot$(6)="SIX!"
out$(0)="CAUGHT":out$(1)="BOWLED":out$(2)="STUMPED"
out$(3)="RUNOUT":out$(4)="RETIRED":out$(5)="HANDLEDBALL"

Function.s nth{n.w}
	a$=Str$(n):If n>20 Then n=n MOD 10
	Select n
		Case 1:a$+"st"
		Case 2:a$+"nd"
		Case 3:a$+"rd"
		Default:a$+"th"
	End Select
	Function Return a$
End Function

.main:
	;
	Gosub readteam
	Gosub setuppalette
	Gosub setupscreen
	Gosub drawoval
	Gosub initevents
	Gosub printevents

	Gosub printstats

	;
	batting=0

	;
	Repeat
		;
		ev.l=WaitEvent
		id=-1:If ev=$20 Then id=GadgetHit
		;
		If id=69 Then End
		;
		Select entry(cmess)\id
			Case 0:																				 ;please toss
				If id=50 Then Gosub inittoss
			Case 1:																				 ;toss
				If id=0 OR id=20 Then Gosub tossinput
				If id=72 AND tossok=1 Then Gosub initinnings
			Case 2:																				 ;innings
				If id>0 AND id<13 Then Gosub inningsinput
				If id=72 AND inningsok=1 Then Gosub initover
			Case 3:																				 ;over
				If id>20 AND id<33 OR id=70 Then Gosub overinput
				If id=72 AND overok=1 Then ball=0:Gosub nextball
			Case 4:																				 ;ball
				If id=71 Then face=1-face:Gosub facing
				If id>52 AND id<60 Then shot=id-53:Gosub scoring
				If id>59 AND id<67 Then Gosub initout
				If ev=$8 AND EventWindow=1 Then Gosub ovalclick
				If id=72 AND ballok=1 Then Gosub ballok
			Case 5																					;out!
				If id>20 AND id<33 Then Gosub outinput
				If id>52 AND id<60 Then shot=id-53:Gosub scoring ;undo out?
				If id>59 AND id<67 Then Gosub initout
				If id=72 AND outok=1 Then Gosub outok
			Case 6																					;out!
				If id>0 AND id<13 Then Gosub newbatterinput
				If id=72 AND newbatterok=1 Then Gosub newbatterok
				;
		End Select
		;
	Forever

.inittoss:
	cmess=nxmess:nxmess+1:moff+1
	entry(cmess)\message="TOSS: ***SELECT TEAM TO BAT***",1
	tossok=0:Gosub printevents:Return

.tossinput:
	WaitEvent
	If id<>0 Then batting=1-batting
	bowling=1-batting
	For i=0 To 11
		For j=0 To 1
			team(j)\ba[i]=i:team(j)\bo[i]=i
		Next
	Next
	Gosub listplayers
	entry(cmess)\message="TOSS: "+team(batting)\team\teamname+" IS BATTING"
	entry(cmess)\batting=batting:tossok=1:Gosub printevents:Return

.initinnings:
	cmess=nxmess:nxmess+1:moff+1
	entry(cmess)\message="INNINGS: ***SELECT OPENING BATSMEN***",2
	openers=0:inningsok=0:Gosub printevents:Return

.inningsinput:
	USEPATH team(batting)
	WaitEvent
	b=\ba[id-1]:entry(cmess)\batter[openers]=b:bat(openers)=b
	p=openers:b=id-1:If b<>p Then Exchange \ba[p],\ba[b]
	openers=1-openers:If openers=0 Then inningsok=1:over=1
	Gosub listplayers
	entry(cmess)\message="INNINGS: "+\team\name[bat(0)]+"&"+\team\name[bat(1)]+" OPENING BATSMEN"
	Gosub printevents
	Return

.initover:
	cmess=nxmess:nxmess+1:moff+1:overok=0
	t$=nth{over}:entry(cmess)\message=t$+" OVER: ***SELECT BOWLER AND END***",3
	Gosub printevents
	Return

.overinput:
	USEPATH team(bowling)
	WaitEvent
	If id=70
		near=1-near
		entry(cmess)\whichend=near
	Else
		id-21:bowler=\bo[id]					;look up order
		entry(cmess)\bowler=bowler		;set entry
		oldid=id
		If \bowl[bowler]\balls=0
			jb=0
			Repeat
				id-1:If id>-1 Then jb=\bowl[\bo[id]]\balls<>0
			Until id<0 OR jb<>0
			If jb<>0 Then id+1
			If id<0 Then Then id=0
			Exchange \bo[id],\bo[oldid]
		EndIf
		Gosub listplayers
		overok=1
	EndIf
	t$=nth{over}:tt$=\team\name[bowler]
	entry(cmess)\message=t$+" OVER: "+tt$+" BOWLING FROM "+ends$(near)
	Gosub drawoval
	Gosub printevents
	Return

.initout:
	out=id-60
	t$=team(batting)\team\name[bat(face)]+" "+out$(out)
	If out=0 OR out=2 OR out=3
		t$+" FIELDER ???":outok=0
	Else
		outok=1
	EndIf
	entry(cmess)\message=t$,5
	Gosub printevents
	Return

.outinput:
	t$=team(batting)\team\name[bat(face)]+" "+out$(out)
	fielder=team(bowling)\bo[id-21]
	t$+" BY "+team(bowling)\team\name[fielder]
	entry(cmess)\message=t$
	outok=1
	Gosub printevents
	Return

.outok:
	entry(cmess)\reason=out,fielder
	Goto newbatter

.newbatter:
	cmess=nxmess:nxmess+1:moff+1:newbatterok=0
	entry(cmess)\message="SELECT NEW BATTER",6
	Gosub printevents
	Return

.newbatterinput:
	USEPATH team(batting)
	batter=\ba[id-1]
	newbatterok=1
	t$="NEW BATTER IS "+team(batting)\team\name[batter]
	entry(cmess)\message=t$
	Gosub printevents
	Return

;	b=\ba[id-1]:entry(cmess)\batter[openers]=b:bat(openers)=b
;	p=openers:b=id-1:If b<>p Then Exchange \ba[p],\ba[b]
;	openers=1-openers:If openers=0 Then inningsok=1:over=1
;	Gosub listplayers
;	entry(cmess)\message="INNINGS: "+\team\name[bat(0)]+"&"+\team\name[bat(1)]+" OPENING BATSMEN"

.newbatterok:
	entry(cmess)\newbatter=batter
	Goto nextball

.ballok:
	entry(cmess)\facing=face,r,x,shot,ex,ey
	If shot=1 OR shot=2 Then ball-1
	Goto nextball

.nextball:
	ball+1:If ball=7 Then Goto initover
	cmess=nxmess:nxmess+1:moff+1:shot=0:runs=0:ballok=0
	Gosub facing:Gosub drawoval:Return

.scoring:
	Select shot
		Case 0:r=runs:x=0	;normal
		Case 1:r=0:If runs<2 Then x=2 Else x=runs	;noball
		Case 2:r=0:If runs<2 Then x=2 Else x=runs	;wide
		Case 3:x=runs:r=0													;bye
		Case 4:x=runs:r=0													;legbye
		Case 5:r=4:x=0														 ;four
		Case 6:r=6:x=0														 ;six
	End Select
	t$="BALL#"+Str$(ball)+": "+team(batting)\team\name[bat(face)]
	t$+" "+shot$(shot)+" RUNS="+Str$(r)+" EXTRAS="+Str$(x)
	entry(cmess)\message=t$,4
	Gosub printevents
	Activate 1
	ballok=1
	Return

.facing:
	t$=team(batting)\team\name[bat(face)]
	entry(cmess)\message="BALL#"+Str$(ball)+": "+t$+" FACING",4
	Gosub printevents
	Return

.ovalclick:
	If EMouseX<0 OR EMouseX>191 OR EMouseY<0 OR EMouseY>109 Then Return
	Use Window 1
	mb=MButtons
	x=QLimit(EMouseX-4,0,184)
	y=QLimit(EMouseY-4,0,102)
	If ballok<>0
		If mb=1 Then runs=QLimit(runs+1,0,9)
		If mb=2 Then runs=QLimit(runs-1,0,9)
	EndIf
	ShapesBitMap 0,1
	Use BitMap 1:BitMapOutput 1
	Line ex,ey,x,y,1
	Locate x/8,y/8:Print runs
	BitMaptoWindow 1,1
	WaitEvent
	WindowOutput 0
	Gosub scoring
	Return

.printstats:
	Use Window 0:WindowOutput 0
	WLocate 138,129:Print "BATTER"
	WLocate 230,129:Print " B	 R	 4	6	M "
	WLocate 230,139:Print "100 122 10 10 123"
	WLocate 414,129:Print "BOWLER"
	WLocate 492,129:Print " O M	R	 W	X	B nb	b lb	w"
	WLocate 492,139:Print "10 8 160 12 32 40 40 10 10 20"
	Return

.setupscreen
	Screen 0,0,0,736,283,3,$8000,"HELLO",1,2:FadePalette 0
	USEPATH team(x)\team

	For x=0 To 1
		t$=LSet$(\teamname,20)+"("+LSet$(\teamabbrev,2)+")"
		TextGadget 0,4,99+x*13,0,x*20,t$ ;x*262+120,2,0,x*20,t$
		For i=0 To 11
			TextGadget 0,x*262+120,i*12+137,0,x*20+i+1,LSet$(\name[i],12)
		Next
	Next
	;
	Restore funcs:id=50
	For x=0 To 1
		For i=0 To 9
			Read a$:a$=LSet$(a$,7):m=0;:If id>52 AND id<60 Then m=1
			TextGadget 0,598+x*68,3+i*12,m,id,a$:id+1
		Next
	Next
	;
	TextGadget 0,398,113,0,70,"ENDS"
	TextGadget 0,442,113,0,71,"FACE"
	TextGadget 0,486,113,0,72,"		 OK		 "
	;
	PropGadget 0,0,0,$80,100,12,96
	;
	Window 0,0,0,736,283,$1800,"",1,2,0:MenusOff
	;
	ResetString 18,2,370,92

	ResetString 4,99,384,25

	ResetString 398,2,189,108

	ResetString 598,2,132,122

	ResetString 4,129,102,152
	ResetString 118,129,250,152
	ResetString 380,129,350,152
	;
	Window 1,397,1,192,110,$800,"",1,2:MenusOff
	;
	BitMap 0,192,110,3
	BitMap 1,192,110,3
	;
	Return

.funcs:
	Data.s TOSS,INNINGS,OVER,NORMAL,NOBALL,WIDE,BYE,LEGBYE,FOUR,SIX
	Data.s CAUGHT,BOWLED,STUMPED,RUNOUT,RETIRED,HITWICKET,HANDLEDBALL,,,QUIT

.drawoval:
	Use BitMap 0:BlockScroll 3
	Circlef 95,54,80,53,4
	Boxf 95-10,55-12,95+10,55+12,5
	ends=near:ex=94:ey=44+ends*21
	For x=-1 To 1:Boxf ex+x*6,ey,ex+2+x*6,ey+1,1:Next
	ShapesBitMap 0,1:BitMaptoWindow 1,1
	Return

.setuppalette
	StopCycle 0,0,8,8,8:StopCycle 0,1,15,15,15:StopCycle 0,2,0,0,0:StopCycle 0,3,4,8,12
	StopCycle 0,4,4,12,4:StopCycle 0,5,0,8,0:StopCycle 0,6,8,4,12:StopCycle 0,7,8,8,15
	Return

.readteam
	If WriteMem(0,"teams")
		FileInput 0
		USEPATH team(i)\team
		For i=0 To 1
			\teamname=Edit$(80)
			\teamabbrev=Edit$(80)
			For j=0 To 11:\name[j]=Edit$(80):Next
		Next
		CloseFile 0
	Else
		NPrint "Couldn't Open Team File!":End
	EndIf
	Return

.printevents
	Use Window 0:WindowOutput 0
	moff=QLimit(cmess-9,0,4096)
	For i=0 To 9
		ii=moff+i:If ii=cmess Then col=1 Else col=2
		WColour col:WLocate 20,2+i*9:Print LSet$(entry(ii)\message,46)
	Next
	WColour 1,0
	Return

.listplayers
	USEPATH team(batting)
	;
	t$=LSet$(\team\teamname,20)+"("+LSet$(\team\teamabbrev,2)+")"
	##[$C622] 0,0,t$
	For i=0 To 11
		##[$C622] 0,i+1,LSet$(\team\name[\ba[i]],12)
	Next
	;
	USEPATH team(bowling)
	;
	t$=LSet$(\team\teamname,20)+"("+LSet$(\team\teamabbrev,2)+")"
	##[$C622] 0,20,t$
	For i=0 To 11
		##[$C622] 0,i+21,LSet$(\team\name[\bo[i]],12)
	Next
	;
	For i=0 To 12:Redraw 0,i:Redraw 0,20+i:Next
	;
	Return

.initevents:
	Restore header
	For i=0 To 6:Read entry(i)\message:Next
	nxmess=7:cmess=5:Return

.header						;46 chars wide
	Data.s "----------------------------------------------"
	Data.s "			 SKY CRICKET STATISTICS SOFTWARE				"
	Data.s "																							"
	Data.s "		(C)1994 ARMSTRONG COMMUNICATIONS LTD			"
	Data.s "																							"
	Data.s "					 SELECT TOSS TO START							 "
	Data.s "----------------------------------------------"
