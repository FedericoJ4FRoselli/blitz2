; ascii translation of /Users/simon/Google Drive/amiga/w/disks/bums/bum7/disk1/code/subdaba.bb,/Users/simon/Google Drive/amiga/w/lha/bbold/disks/bums/bum7/disk1/code/subdaba.bb
;----=================================================----
;----====																				 ====----
;----====								 SubDaBa								 ====----
;----====					Subscription DataBase					====----
;----====																				 ====----
;----====	 A database for keeping and printing	 ====----
;----====	mail labels for newsletters and mags.	====----
;----====																				 ====----
;----==== THIS PROGRAM REQUIRES BLITZ VERSION 1.8 ====----
;----====				AND MOST LIBS UPTO BUM 6				 ====----
;----====																				 ====----
;----====								written by							 ====----
;----====							David Maziarka						 ====----
;----====																				 ====----
;----====				 FreeWare..Copyright 1994				====----
;----====																				 ====----
;----====	You may alter the code and use parts	 ====----
;----====	in your own programs..but you may NOT	====----
;----====	sell any part of this code or the			====----
;----====	program as a whole for profit.				 ====----
;----====																				 ====----
;----=================================================----

;amigalibs.res NEEDS TO BE RESIDENT!

;YOU WILL NOTICE THAT I USE EasyRequestArgs_ AND THE
;Elmore VERSION...THIS IS BECAUSE I STARTED BY DOING
;THE REQUESTER MYSELF AND HAVE SINCE SWITCHED TO USING
;Elmore's.

;IF YOU PLAN TO MAKE AN EXECUTABLE...REMOVE ALL THE
;'WBenchToBack_' STATEMENTS.

;Most of program is pretty easy to understand, so I'm
;not going to document most of it. The gadgets in the
;main window are self expanitory except for the 'Perm',
;'Temp' and string gadget under the listview. The Perm
;and Temp gadgets that can be set to mark certain entries
;to indicate something or for printing. The Perm gadget
;will be saved with the entry while the Temp will be
;restored whenever you reload or new the current base.
;The string under the listview is for searching the base
;for certain entries, it checks only names. The menu option
;'Sort' uses 'Cvl' to get a long word that describes the
;first 4 letters of the name....so the sort ONLY sorts
;by the first 4 letters. When entering data, pressing return
;will send you to the next string but you may also use the
;standard gadtools keys too. those keys are 'tab' which
;sends you to the next string and 'shift tab' which sends
;you to the previous string.
;
;Printing....Since I use Pagestream for my printing the
;routines in this program are simple. In the print window
;there are a set of cycle gadgets...use these to format
;the printouts. What I do is use the PS template (which
;I included in the archive) and print with the default
;foramt settings to a file. From PS I do a text import
;(ascii, paragraph has linefeeds) of the file printed by
;SubDaBa. Doing it this way gives me more flexability and
;font control.
;
;Well, I hope you can figure out the rest...If you wish to
;contact me about this program leave a message on the
;North American Blitz Support BBS or send me mail on
;BLITZ_AMY.
;
;North American Blitz Support BBS
;608-257-9057 6pm-6am CST (-6 from GMT)
;up all the time on weekends



NoCli
WBStartup
DateFormat 1
CaseSense Off


##[$FFB5]
WbToScreen 0

DEFTYPE.Screen *wbscreen
DEFTYPE.Window *win
DEFTYPE.EasyStruct req
req\es_StructSize=SizeOf .EasyStruct


*wbscreen=##[$FFD1](0) ;	 THIS LINE TELLS US HOW MUCH
bh.b=*wbscreen\BarHeight-10 ;<--TALLER THEIR SCREEN FONT IS
ww.w=*wbscreen\Width				;	 VS. A FONT WITH A HEIGHT OF
wh.w=*wbscreen\Height			 ;	 8

;IF YOU USE LockPubScreen ALWAYS REMEMBER TO DO A
;UnlockPubScreen BEFORE YOU END !!!!


SetErr
	 PutReg d0,err.l
	 title$="ERROR:"+Chr$(0)
	 gadget$="OK"+Chr$(0)
	 req\es_Title=&title$,err,&gadget$
	 FindScreen 0:BeepScreen 0
	 ##[$FFDD] 0,req,0,0
	 ##[$FFD2] 0,*wbscreen
	 End
End SetErr

;MAKE SURE WORKBENCH IS TALL ENOUGH FOR OUR WINDOW (INCLUDING
;THEIR SCREEN FONT). IF IT IS NOT THEN SHOW REQUESTER AND
;END.

If wh<208+bh OR ww<500
	 title$="Startup Error"+Chr$(0)
	 reqtext$="SubDaBa Needs A Screen Height of "+Str$(208+bh)+Chr$(0)
	 gadget$="OK"+Chr$(0)
	 req\es_Title=&title$,&reqtext$,&gadget$
	 ##[$FFDD] 0,req,0,0
	 ##[$FFD2] 0,*wbscreen
	 End
EndIf


;REFRESHWINDOW =$4				 ;YOU NEED amigalibs.res FOR THIS
:MOUSEMOVE		 =$10				;PROGRAM AND IN IT THESE ARE DEFINED.
;GADGETDOWN		=$20				;I JUST USED THE REAL (BELOW) NUMBERS
;GADGETUP			=$40				;SO I COULD GET THEM TO FIT ON ONE
;MENUPICK			=$100			 ;LINE.
;CLOSEWINDOW	 =$200			 ;
;ACTIVEWINDOW	=$40000		 ;
;INACTIVEWINDOW=$80000
;INTUITICKS		=$400000

#IDCMP=$4|$10|$20|$40|$100|$200|$40000|$80000|$400000

#tag=$80080000
#strtag=$80032000

.vars

NEWTYPE .entry
	 bud.w
	 info$[11]
;	 0 name$
;	 1 street$
;	 2 city$
;	 3 state$
;	 4 country$
;	 5 zip$
;	 6 phone$
;	 7 datereg$					; date person filled out the reg. form
;	 8 dateputin$				; date put in the database
;
;	 9 notes1$
;	 10 notes2$

	 subscriber.b				 ;THIS IS NOW THE "Perm" GADGET.
	 numleft.b

	 mark.b							 ;THIS IS THE "Temp" GADGET.
	 listsort.l
End NEWTYPE

Dim List people.entry(200)
USEPATH people()


;ONE IMPORTANT THING TO REMEMBER ABOUT 'ListView' GADGETS AND
;THE 'lists' THEMSELVES IS THAT THE GADGET READS THE MEMORY
;SEQUENTIALLY BUT THE LISTS COULD BE STORED IN ANY ORDER
;IF YOU USE "AddItem". SO ALWAYS USE "AddLast" or "AddFirst"!


currententry.w=0				;I USE THESE FOR MY LISTVIEW.
lastentry.w=0					 ;

subscriber.b=0
numleft.b=0
mark.b=0

MaxLen path$=192
MaxLen filename$=192

printto.b=1

printi2.b=1
printi1.b=0
printi0.b=0
printperm.b=0
printtemp.b=0

Dim printwhat.b(10,1)
printwhat(0,0)=0:printwhat(0,1)=1
printwhat(1,0)=1:printwhat(1,1)=1
printwhat(2,0)=2:printwhat(2,1)=1
printwhat(3,0)=3:printwhat(3,1)=4
printwhat(4,0)=4:printwhat(4,1)=2
printwhat(5,0)=5:printwhat(5,1)=2
printwhat(6,0)=6:printwhat(6,1)=1
printwhat(7,0)=0:printwhat(7,1)=1
printwhat(8,0)=0:printwhat(8,1)=1
printwhat(9,0)=12:printwhat(9,1)=1
printwhat(10,0)=0:printwhat(10,1)=1

MaxLen printfile$=192:printfile$="PRT:"
MaxLen printpath$=192:printpath$=""

;doprint variables
Dim labelline$(5),row$(5)

LoadFont 1,"helvetica.font",11:Use IntuiFont 1

;SET UP THE GADGET AND MENU LISTS.
Gosub menu0
Gosub gadgets0
Gosub gadgets1


DefaultIDCMP #IDCMP
i=wh/2-((208+bh)/2)											 ;CENTER THE WINDOW BUT
i=QLimit(i-i/4,0,400)										 ;PLACE IT UP A LITTLE
Window 0,ww/2-250,i,500,208+bh,$2|$4|$8|$1000|$00200000,"SubDaBa by David Maziarka",2,1,0
AttachGTList 0,0

GTBevelBox 0,4,11+bh,314,125,0
GTBevelBox 0,318,11+bh,178,147,0
GTBevelBox 0,4,136+bh,314,22,0
GTBevelBox 0,4,158+bh,492,48,0

StringText$ 0,0										;THE LITTLE INDENTED
SetString 1,2								 ;BOX NEXT THE THE
ResetString 402,165+bh,18,14	;# OF ISSUES SCROLL BAR

d$=Left$(Date$(SystemDate),6)+Right$(Date$(SystemDate),2)
GTSetString 0,9,d$

SetMenu 0
MenuColour 0,0,1,Off:MenuColour 0,0,3,Off
MenuColour 0,0,6,Off:MenuColour 0,0,8,Off


.fillstrings
Statement fillstrings{clear,updatelist}			;CLEARS THE WINDOW
	 ;IF clear=1 THEN CLEAR THE STRINGS				;OR FILLS IT WITH
	 Shared people(),currententry,lastentry		;THE CURRENT ENTRY
	 Shared numleft,subscriber,experience
	 Shared udi,mark

	 If updatelist
			GTSetAttrs 0,19,#tag+5,QLimit(currententry-4,0,lastentry)
	 EndIf
	 GTSetAttrs 0,19,#tag+54,currententry

	 If clear
			For i=0 To 8
				 GTSetString 0,i+1,""
			Next
			For i=9 To 10
				 GTSetString 0,i+11,""
			Next

			numleft=0
			subscriber=0
			mark=0

			GTSetAttrs 0,23,#tag+40,0
			GTSetAttrs 0,22,#tag+4,0
			GTSetAttrs 0,24,#tag+4,0
	 Else
			For i=0 To 8
				 GTSetString 0,i+1,\info[i]
			Next
			For i=9 To 10
				 GTSetString 0,i+11,\info[i]
			Next

			numleft=\numleft
			subscriber=\subscriber
			mark=\mark

			GTSetAttrs 0,23,#tag+40,\numleft
			GTSetAttrs 0,22,#tag+4,\subscriber
			GTSetAttrs 0,24,#tag+4,\mark
	 EndIf

	 ClearString 0,1
End Statement

.update
Statement update{}				 ;UPDATES FROM THE WINDOW TO THE LIST
	 Shared people(),numleft,subscriber,experience,udi,mark

	 For i=0 To 8
			\info[i]=GTGetString(0,i+1)
	 Next
	 For i=9 To 10
			\info[i]=GTGetString(0,i+11)
	 Next

	 \numleft=numleft
	 \subscriber=subscriber
	 \mark=mark

	 GTChangeList 0,19
	 GTChangeList 0,19,people()
End Statement

.decrease
Statement decrease{}							;DECREASES THE NUMBER OF
	 Shared people()								;ISSUES FOR EACH ENTRY

	 PushItem people()
	 ResetList people()
	 While NextItem(people())
			\numleft=QLimit(\numleft-1,0,100)
	 Wend
	 PopItem people()
	 fillstrings{False,False}

End Statement

.search
Statement search{}								;DOES A SEARCH FROM THE
	 Shared people(),currententry	 ;STRING UNDER THE LISTVIEW
	 Shared req

	 oldentry=currententry

	 search$=GTGetString(0,18)
	 ResetList people()
	 i=0
	 While NextItem(people())

			If Instr(\info[0],search$)
				 currententry=i
				 GTSetAttrs 0,19,#tag+5,i
				 fillstrings{0,1}

				 title$="Search"+Chr$(0)
				 reqtext$="Continue Search for "+search$+Chr$(0)
				 gadget$="OK|STOP|CANCEL"+Chr$(0)
				 req\es_Title=&title$,&reqtext$,&gadget$
				 temp=##[$FFDD](0,req,0,0)

				 If temp=2 Then Statement Return
					If temp=0 Then Goto out
			EndIf
			i+1
	 Wend
out:
	 currententry=oldentry
	 FirstItem people()
	 For i=1 To currententry:NextItem people():Next
	 GTSetAttrs 0,19,#tag+5,currententry
	 fillstrings{0,1}
End Statement


.save
Statement save{}
	 Shared people(),filename$
	 update{}

	 If ##[$BE15](filename$+".SDB")
			temp$=filename$+".SDB"+Chr$(0)
			##[$FE08] temp$
	 EndIf


	 If OpenFile(0,filename$+".SDB")
			FileOutput 0

			PushItem people()
			ResetList people()
			t=0
			While NextItem(people())
				 For i=0 To 8:NPrint \info[i]:Next
				 For i=9 To 10:NPrint \info[i]:Next

				 NPrint Str$(\numleft)
				 NPrint Str$(\subscriber)

				 NPrint "--------"

				 t+1
			Wend
			WindowOutput 0
			CloseFile 0
			PopItem people()
	 EndIf
End Statement


.open
Statement open{filename$}
	 Shared people(),currententry,lastentry
	 Shared numleft,subscriber,experience,udi

	 If OpenFile(0,filename$)
			FileInput 0
			ClearList people()
			ResetList people()
			t=0
			While NOT DosBuffLen(0)
				 If AddLast(people())
						For i=0 To 8:\info[i]=Edit$(80):Next
						For i=9 To 10:\info[i]=Edit$(80):Next

						\numleft=Val(Edit$(80))
						\subscriber=Val(Edit$(80))

						t$=Edit$(80)

						t+1
				 EndIf
			Wend
			WindowOutput 0
			CloseFile 0
			currententry=0
			lastentry=t-1
			FirstItem people()
			fillstrings{1,0}
			fillstrings{0,1}
			GTChangeList 0,19
			GTChangeList 0,19,people()
	 EndIf
End Statement


.sortpeople
Statement sortpeople{}
	 Shared people(),currententry

	 ResetList people()
	 i.w=0
	 While NextItem(people())
			\listsort=Cvl(\info[0])
			i+1
	 Wend
	 pnum=i

	 SortList people(),SizeOf .entry\listsort
	 ResetList people()

	 currententry=0
	 FirstItem people()
	 fillstrings{1,0}
	 fillstrings{0,1}
	 GTChangeList 0,19
	 GTChangeList 0,19,people()

End Statement


ResetList people()
AddItem people()
ClearString 0,1
currententry=0
lastentry=0
GTChangeList 0,19
GTChangeList 0,19,people()
GTSetAttrs 0,19,#tag+54,currententry

.main
	 ev.l=WaitEvent
	 gh.l=GadgetHit
	 code.l=EventCode
	 menu.l=MenuHit
	 mitem.l=ItemHit

	 Select ev
			Case 512					 ;#CloseWindow
				 Gosub quit
			Case #GADGETDOWN
				 If gh<10 Then currentstring=gh
				 If gh>19 AND gh<22 Then currentstring=gh

			Case #GADGETUP
				 If gh<10
						If gh=1					 ;update listview if return on name$
							 \info[0]=GTGetString(0,1)
							 GTSetString 0,18,\info[0]
							 GTChangeList 0,19
							 GTChangeList 0,19,people()
							 GTSetAttrs 0,19,#tag+5,QLimit(currententry-4,0,lastentry)
							 GTSetAttrs 0,19,#tag+54,currententry
						EndIf
						currentstring=QWrap(gh+1,1,10)
						ClearString 0,currentstring
				 EndIf
				 If gh>19 AND gh<22
						currentstring=QWrap(gh+1,1,22)
						ClearString 0,currentstring
				 EndIf

				 Select gh
						Case 10					 ;first
							 update{}
							 If FirstItem(people())
									currententry=0
									fillstrings{0,1}
							 EndIf
						Case 11					 ;prev
							 update{}
							 If PrevItem(people())
									currententry-1
									fillstrings{0,1}
							 EndIf
						Case 12					 ;next
							 update{}
							 If NextItem(people())
									currententry+1
									fillstrings{0,1}
							 EndIf
						Case 13					 ;last
							 update{}
							 If LastItem(people())
									currententry=lastentry
									fillstrings{0,1}
							 EndIf

						Case 14					 ;create
							 update{}
							 If AddLast(people())
									GTChangeList 0,19
									GTChangeList 0,19,people()
									lastentry+1
									currententry=lastentry
									fillstrings{1,1}
									d$=Left$(Date$(SystemDate),6)+Right$(Date$(SystemDate),2)
									GTSetString 0,9,d$
							 EndIf
						Case 15					 ;delete
							 title$="Delete Entry"+Chr$(0)
							 reqtext$="Delete "+GTGetString(0,1)+Chr$(0)
							 gadget$="OK|CANCEL"+Chr$(0)
							 req\es_Title=&title$,&reqtext$,&gadget$
							 If ##[$FFDD](0,req,0,0)=1
									KillItem people()
									currententry=currententry-1
									lastentry-1
									GTChangeList 0,19
									GTChangeList 0,19,people()
									fillstrings{0,0}
									ClearString 0,1
							 EndIf

						Case 18					 ;search
							 update{}
							 search{}
						Case 19					 ;pick from listview
							 update{}
							 If FirstItem(people())
									currententry=code
									For i=1 To currententry:NextItem people():Next
									GTSetString 0,18,\info[0]
									fillstrings{0,0}
							 EndIf

						Case 22
							 subscriber=code
						Case 23
							 numleft=code
						Case 24
							 mark=code

				 End Select

.memumain
			Case #MENUPICK
				 Select menu
						Case 0
							 Select mitem
									Case 0							 ;new
										 ClearList people()
										 ResetList people()
										 AddItem people()
										 ClearString 0,1
										 currententry=0
										 lastentry=0
										 GTChangeList 0,19
										 GTChangeList 0,19,people()
										 GTSetAttrs 0,19,#tag+54,currententry

									Case 2							 ;open
										 filename$=Replace$(filename$,path$,"")
										 filename$=ASLFileRequest$("Open...",path$,filename$,"#?.SDB",0,0,0,280)
										 If filename$
												open{filename$}
										 EndIf

									Case 4							 ;save
										 update{}
										 If filename$
												filename$=Replace$(filename$,".SDB","")
												save{}
										 Else
												filename$=ASLFileRequest$("Save As...",path$,filename$,"#?.SDB",0,0,0,280)
												If filename$
													 filename$=Replace$(filename$,".SDB","")
													 save{}
												EndIf
										 EndIf
									Case 5							 ;save as
										 filename$=Replace$(filename$,path$,"")
										 filename$=ASLFileRequest$("Save As...",path$,filename$,"#?.SDB",0,0,0,280)
										 If filename$
												filename$=Replace$(filename$,".SDB","")
												save{}
										 EndIf

									Case 7
										 update{}
										 Gosub printwin

									Case 9							 ;about
										 Gosub about

									Case 10							;quit
										 Gosub quit
							 End Select

						Case 1
							 Select mitem
									Case 0							 ;info
										 update{}
										 Gosub info

									Case 1							 ;decease issues
										 update{}
										 If ##[$B58D]("Decrease Issues","Warning: Cannot Undo!","OK|CANCEL")
												decrease{}
										 EndIf
									Case 2
										 update{}
										 sortpeople{}			;do sort
							 End Select
				 End Select

	 End Select
Goto main

.about
	 text$="	Subscription Data Base	"
	 text$+"|			version 1.00			 "
	 text$+"|												 "
	 text$+"|			 written by				"
	 text$+"|		 David Maziarka			"

	 ##[$B58D] "About SuDaBa",text$,"OK"

Return

.info
	 totalentry.w=0
	 totali2.w=0
	 totali1.w=0
	 totali0.w=0
	 totaltemp.w=0
	 totalperm.w=0

	 PushItem people()
	 ResetList people()

	 While NextItem(people())
			totalentry+1
			If \numleft>1 Then totali2+1
			If \numleft=1 Then totali1+1
			If \numleft<1 Then totali0+1
			If \mark=1 Then totaltemp+1
			If \subscriber=1 Then totalperm+1
	 Wend

	 text$=" Total Entries:"+Str$(totalentry)
	 text$+"|		 2+ Issues:"+Str$(totali2)
	 text$+"|			1 Issues:"+Str$(totali1)
	 text$+"|			0 Issues:"+Str$(totali0)
	 text$+"|Marked Entries:"+Str$(totaltemp)
	 text$+"|	Perm Entries:"+Str$(totalperm)

	 ##[$B58D] "SuDaBa Stats",text$,"OK"
	 PopItem people()

Return

.printwin
	 i=wh/2-((200+bh)/2)											 ;CENTER THE WINDOW BUT
	 i=QLimit(i-i/4,0,400)										 ;PLACE IT UP A LITTLE

	 Window 1,ww/2-150,i,300,200+bh,$2|$4|$1000|#WA_AutoAdjust,"Print",2,1,0
	 AttachGTList 1,1

	 GTBevelBox 1,4,11+bh,200,165,0
	 GTBevelBox 1,4,176+bh,292,22,0
	 GTBevelBox 1,204,11+bh,92,46,0
	 GTBevelBox 1,204,57+bh,92,119,0

	 GTSetAttrs 1,12,#tag+10,printto

	 GTSetAttrs 1,7,#tag+4,printi2
	 GTSetAttrs 1,8,#tag+4,printi1
	 GTSetAttrs 1,9,#tag+4,printi0
	 GTSetAttrs 1,10,#tag+4,printtemp
	 GTSetAttrs 1,11,#tag+4,printperm

	 For i=0 To 10
			GTSetAttrs 1,13+i,#tag+15,printwhat(i,0)
			GTSetAttrs 1,24+i,#tag+15,printwhat(i,1)
	 Next

	 Repeat
			ev.l=WaitEvent
			gh.l=GadgetHit
			code.l=EventCode

			Select ev
				 Case #GADGETDOWN
						Select gh
							 Case 12
									printto=code
						End Select

				 Case #GADGETUP
						Select gh
							 Case 2							 ;print
									If printto=1
										 printfile$=Replace$(printfile$,"PRT:","")
										 printfile$=Replace$(printfile$,printpath$,"")
										 printfile$=ASLFileRequest$("Print As...",printpath$,printfile$,"#?.txt",0,0,0,280)
										 If printfile$
												If Instr(printfile$,".txt")=False
													printfile$+".txt"
												EndIf
												Gosub doprint
												ev=#GADGETUP:gh=1
										 EndIf
									Else
										 printfile$="PRT:"
										 Gosub doprint
										 ev=#GADGETUP:gh=1
									EndIf

							 Case 7
									printi2=code
							 Case 8
									printi1=code
							 Case 9
									printi0=code
							 Case 10
									printtemp=code
							 Case 11
									printperm=code


						End Select
						If gh>12 AND gh<24
							 printwhat(gh-13,0)=code
						EndIf
						If gh>23 AND gh<35
							 printwhat(gh-24,1)=code
						EndIf
			End Select

	 Until ev=#GADGETUP AND gh=1
	 CloseWindow 1
Return

.doprint
If ReadMem(0,printfile$)
	 FileOutput 0

	 numprinted=0
	 PushItem people()
	 ResetList people()

	 Repeat
			If NextItem(people())
				 numprinted+1
				 printentry=False

				 If \subscriber=1 AND printperm=1 Then printentry=True
				 If \mark=1 AND printtemp=1 Then printentry=True

				 If \numleft>1 AND printi2=1 Then printentry=True
				 If \numleft=1 AND printi1=1 Then printentry=True
				 If \numleft<1 AND printi0=1 Then printentry=True

				 If printentry=True
						For i=0 To 10
							 Select printwhat(i,0)
									Case 1							 ;name
										 place=Instr(\info[0],",")
										 temp$=Mid$(\info[0],place+2)+" "+Left$(\info[0],place-1)
										 Print temp$
									Case 2							 ;street
										 Print \info[1]
									Case 3							 ;city
										 Print \info[2]
									Case 4							 ;state
										 Print \info[3]
									Case 5							 ;country
										 Print \info[4]
									Case 6							 ;zip
										 Print \info[5]
									Case 7							 ;phone
										 Print \info[6]
									Case 8							 ;dates
										 Print "Registered:",\info[7]
										 Print " Recorded:",\info[8]
									Case 9							 ;issues left
										 Print \numleft
									Case 10							;note a
										 Print \info[9]
									Case 11							;note b
										 Print \info[10]
									Case 12							;last issue
										 Gosub lastissue
							 End Select

							 Select printwhat(i,1)
									Case 1
										 Print Chr$(10)+Chr$(13)
									Case 2
										 Print " "
									Case 3
										 Print ","
									Case 4
										 Print ", "
									Case 5
										 Print ":"
									Case 6
										 Print ": "
							 End Select

						Next
				 EndIf

			EndIf

	 Until numprinted>lastentry
	 CloseFile 0
	 WindowOutput 0
	 PopItem people()
EndIf
Return

.lastissue
	 If \numleft=1
			Print "LAST ISSUE - LAST ISSUE - LAST ISSUE"
	 EndIf

	 If \numleft=0
			Print "SUBSCRIPTION FORM ENCLOSED"
	 EndIf

Return


.quit
	 title$="SubDaBa"+Chr$(0)
	 reqtext$="Quit SubDaBa?"+Chr$(0)
	 gadget$="OK|CANCEL"+Chr$(0)
	 req\es_Title=&title$,&reqtext$,&gadget$
	 If ##[$FFDD](0,req,0,0)=0 Then Return

	 ##[$FFD2] 0,*wbscreen
End

.gadgets1
	 GTButton 1,1,216,168,64,16," Cancel ",0
	 GTButton 1,2,10,168,64,16," Print ",0

	 GTCheckBox 1,7,260,60,19,14,"2+ Issues ",$201
	 GTCheckBox 1,8,260,78,19,14,"1 Issue",$201
	 GTCheckBox 1,9,260,96,19,14,"0 Issues",$201
	 GTCheckBox 1,10,260,114,19,14,"Temp",$201
	 GTCheckBox 1,11,260,132,19,14,"Perm",$201

	 GTMX 1,12,260,6,10,10,"",1,"Printer|File"

	 temp$="Nothing|Name|Street|City|State|Country|Post Code|Phone|Dates|Issues|Note A|Note B|Last Issue"

	 For i=0 To 10
			GTCycle 1,13+i,8,5+i*14,100,14,"",0,temp$
			GTCycle 1,24+i,110,5+i*14,76,14,"",0,"Nothing|LF+CR|Space|Comma|Cmm+Spc|Colon|Cln+Spc"
	 Next

Return

.gadgets0
	 GTString 0,1,60,6,240,16,"Name",1,80
	 GTString 0,2,60,24,240,16,"Street",1,80
	 GTString 0,3,60,42,120,16,"City",1,80
	 GTString 0,4,236,42,64,16,"State",1,10
	 GTString 0,5,60,60,120,16,"Country",1,40
	 GTString 0,6,236,60,64,16,"Zip",1,20
	 GTString 0,7,60,78,240,16,"Phone",1,30
	 GTString 0,8,60,96,64,16,"Date Reg.",1,10
	 GTString 0,9,236,96,64,16,"Date Entered",1,10

	 GTButton 0,10,10,128,32,16," I< ",0
	 GTButton 0,11,42,128,32,16," << ",0
	 GTButton 0,12,74,128,32,16," >> ",0
	 GTButton 0,13,106,128,32,16," >I ",0
	 GTButton 0,14,160,128,64,16," Create ",$20
	 GTButton 0,15,230,128,64,16," Delete ",0

	 ;move string #18 to bottom of list
	 GTListView 0,19,324,6,160,120,"",$0,people()

	 GTString 0,20,60,154,240,16,"A Notes",1,80
	 GTString 0,21,60,172,240,16,"B Notes",1,80

	 GTCheckBox 0,22,428,154,19,14,"Perm",$208
	 f$="%2ld"
	 GTTags #tag+42,&f$,#tag+41,3,#tag+39,12,#tag+43,2
	 GTSlider 0,23,314,154,80,14,"# Of Issues Left",$8,0,12

	 GTCheckBox 0,24,458,154,19,14,"Temp",$208

	 ;moved here so when tab is pressed it will be the
	 ;last string in the list
	 GTString 0,18,324,128,160,16,"",1,80

Return

.menu0


	 If ExecVersion=39
			MenuChecked 1
	 Else
			MenuChecked 2
	 EndIf

;	I needed To do this a$ thing because 'String$' is broken
;	under version 1.8
	 For i=0 To 15
			a$=a$+Chr$(45)
	 Next

	 MenuGap 4,0
	 MenuTitle 0,0,"Project"
	 MenuItem 0,0,0,0,"New"
	 MenuItem 0,0,0,1,a$
	 MenuItem 0,0,0,2,"Open..."
	 MenuItem 0,0,0,3,a$
	 MenuItem 0,0,0,4,"Save"
	 MenuItem 0,0,0,5,"Save As..."
	 MenuItem 0,0,0,6,a$
	 MenuItem 0,0,0,7,"Print..."
	 MenuItem 0,0,0,8,a$
	 MenuItem 0,0,0,9,"About"
	 MenuItem 0,0,0,10,"Quit"

	 MenuTitle 0,1,"Misc"
	 MenuItem 0,0,1,0,"Info"
	 MenuItem 0,0,1,1,"Decrease Issues	"
	 MenuItem 0,0,1,2,"Sort"



Return