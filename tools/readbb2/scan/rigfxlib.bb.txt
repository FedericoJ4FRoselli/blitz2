; ascii translation of /Users/simon/Google Drive/amiga/w/abcstuff/libsdev/userlibsrc/RIGfxLib.bb
+;#rigfxlib=65

!libheader {#rigfxlib,0,0,0,0}

!afunction {#word}
!args {#word}
!libs
!subs {_palred,0,0}
!name {"PalRed","(colour#)"}

!afunction {#word}
!args {#word}
!libs
!subs {_palgreen,0,0}
!name {"PalGreen","(colour#)"}

!afunction {#word}
!args {#word}
!libs
!subs {_palblue,0,0}
!name {"PalBlue","(colour#)"}

!astatement
!args {#long}
!libs {#palettelib,#ia0|#pd0}
!subs {_PaletteInfo,0,0}
!name {"PaletteInfo","palette#"}

!afunction {#word}
!args {#word}
!libs
!subs {_AGApalred,0,0}
!name {"AGAPalRed","(colour#)"}

!afunction {#word}
!args {#word}
!libs
!subs {_AGApalgreen,0,0}
!name {"AGAPalGreen","(colour#)"}

!afunction {#word}
!args {#word}
!libs
!subs {_AGApalblue,0,0}
!name {"AGAPalBlue","(colour#)"}

!astatement
!args {#word,#quick}
!libs {#palettelib,#ia0|#pd0}
!subs {_PalAdjust,0,0}
!args {#word,#quick,#word,#word}
!libs {#palettelib,#ia0|#pd0}
!subs {_PalAdjust2,0,0}
!name {"PalAdjust","palette#,ratio.q[,start_col,end_col]"}

!astatement
!args {#word,#word,#word,#word}
!libs {#palettelib,#ia0|#pd0}
!subs {_FillPalette,0,0}
!args {#word,#word,#word,#word,#word,#word}
!libs {#palettelib,#ia0|#pd0}
!subs {_FillPalette2,0,0}
!name {"FillPalette","palette#,r,g,b[,start_col,end_col] - fill palette with r,g,b values"}

!astatement
!args {#word,#word,#word,#word}
!libs {#palettelib,#ia0|#pd0}
!subs {_AGAFillPalette,0,0}
!args {#word,#word,#word,#word,#word,#word}
!libs {#palettelib,#ia0|#pd0}
!subs {_AGAFillPalette2,0,0}
!name {"AGAFillPalette","palette#,r,g,b[,start_col,end_col] - fill palette with AGA r,g,b values"}

!libfin

_libfinit:
	RTS

_AGAFillPalette:
	MOVEQ.l	 #0,d4
	MOVEQ.l	 #0,d5
	BRA			 _AGAFillPalette2
_FillPalette:
	MOVEQ.l	 #0,d4
	MOVEQ.l	 #0,d5
_FillPalette2:
	ANDI.b		#$f,d1
	ASL.b		 #4,d1
	ANDI.b		#$f,d2
	ASL.b		 #4,d2
	ANDI.b		#$f,d3
	ASL.b		 #4,d3
_AGAFillPalette2:
	ANDI.l		#$ff,d1
	ROR.l		 #8,d1
	ANDI.l		#$ff,d2
	ROR.l		 #8,d2
	ANDI.l		#$ff,d3
	ROR.l		 #8,d3

	MOVE.l		(a0),a0
	TST.l		 d5
	BNE			 'ok
	MOVE.w		(a0),d5
	SUBQ.w		#1,d5
'ok:
	CMP.w		 d4,d5
	BPL			 'ok2
	RTS
'ok2:
	MOVE.w		d5,d7
	ADDQ.w		#1,d7
	CMP.w		 (a0),d7
	BGE			 'overlimit

	ADDQ.l		#4,a0
	MOVE.w		d4,d7
	MULU.w		#12,d7
	ADD.l		 d7,a0
	SUB.w		 d4,d5
'loop:
	MOVE.l		d1,(a0)+
	MOVE.l		d2,(a0)+
	MOVE.l		d3,(a0)+
	DBRA			d5,'loop
	RTS

'overlimit:
	RTS

_PalAdjust:
	MOVEQ.l	 #0,d2
	MOVE.l		paladdr,a1
	MOVE.w		-4(a1),d3								 ; d3=numcols
	SUB.w		 #1,d3
_PalAdjust2:
	MOVE.l		d2,d6
	MOVE.l		d3,d7

	MOVE.l		d1,d2
	SWAP			d2
	ASL.w		 #8,d2

	ASR.w		 #8,d1
	ANDI.w		#$ff,d1

	ADD.w		 d2,d1
	ANDI.l		#$ffff,d1

	MOVE.l		d1,d2
	MOVE.l		(a0),a1
	ADDQ.l		#4,a1
	MOVE.l		paladdr,a0

	MOVE.w		d6,d5
	MULU.w		#12,d5
	ADD.w		 d5,a0
	ADD.w		 d5,a1

	SUB			 d6,d7
	ADD.w		 #1,d7
	MOVE.w		d7,d1
'ok2:
	MULU.w		#3,d1
	SUBQ.w		#1,d1										 ; adjust NUMCOLS for DBRA

'loop:
	MOVEQ.l	 #0,d0
	MOVE.b		(a0),d0
	MULU.w		d2,d0

	SWAP			d0
	TST.w		 d0
	BEQ			 'ok1
	MOVE.l		#$ff000000,d0

'ok1:
	SWAP			d0
	ASR.w		 #8,d0

	MOVE.b		d0,(a1)
	ADDQ.l		#4,a0
	ADDQ.l		#4,a1

	DBRA			d1,'loop
	RTS


_PaletteInfo:
	MOVE.l	(a0),a0
	ADDQ.l	#4,a0
	MOVE.l	a0,paladdr
	RTS

_palred:
	MOVE.l	paladdr,a0
	MULU.w	#12,d0
	MOVE.b	0(a0,d0.w),d0
	ASR.l	 #4,d0
	ANDI.l	#%1111,d0
	RTS

_palgreen:
	MOVE.l	paladdr,a0
	MULU.w	#12,d0
	MOVE.b	4(a0,d0.w),d0
	ASR.l	 #4,d0
	ANDI.l	#%1111,d0
	RTS

_palblue:
	MOVE.l	paladdr,a0
	MULU.w	#12,d0
	MOVE.b	8(a0,d0.w),d0
	ASR.l	 #4,d0
	ANDI.l	#%1111,d0
	RTS

_AGApalred:
	MOVE.l	paladdr,a0
	MULU.w	#12,d0
	MOVE.b	0(a0,d0.w),d0
	ANDI.l	#$ff,d0
	RTS

_AGApalgreen:
	MOVE.l	paladdr,a0
	MULU.w	#12,d0
	MOVE.b	4(a0,d0.w),d0
	ANDI.l	#$ff,d0
	RTS

_AGApalblue:
	MOVE.l	paladdr,a0
	MULU.w	#12,d0
	MOVE.b	8(a0,d0.w),d0
	ANDI.l	#$ff,d0
	RTS

	Event
paladdr:	Dc.l 0

version: Dc.b "$VER: GFX-Library v1.0 (29/5/1994)",0
