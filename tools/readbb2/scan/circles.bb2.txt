; ascii translation of /Users/simon/Google Drive/amiga/w/abc/bums/bum9/SteveMc/circles.bb2,/Users/simon/Google Drive/amiga/w/abc/bums/old/bum9/SteveMc/circles.bb2,/Users/simon/Google Drive/amiga/w/abcstuff/acid/bums/bum9/SteveMc/circles.bb2,/Users/simon/Google Drive/amiga/w/abcstuff/acid/bums/old/bum9/SteveMc/circles.bb2
;================================================================
; Draw a nice little circle pattern....
; Written by Stephen McNamara of Leading Edge Software
;
; Needs to allocate a LOT of chip mem (256000 bytes!).
; Only run if you have this memory free to allocate, or ensure
; runtime errors are ON.
;================================================================

NEWTYPE .coord
	x.w : y.w
End NEWTYPE

Dim sn.q(359),cn.q(359),polygon.coord(4)

Dim polygon.coord(4)

Statement DrawTriangles {angg.w,radii1.w,radii2.w,col.w}
.MyDrawTri:
	Shared sn(),cn(),polygon()
	BLITZ
	endang.w=angg+360
	Repeat
		angg+10
		ang=angg : ang=ang MOD 360

		polygon(1)\x=160+sn(ang)*radii1
		polygon(1)\y=128+cn(ang)*radii1

		ang2.w=ang+5 : If ang2>359 Then ang2-360
		polygon(2)\x=160+sn(ang2)*radii2
		polygon(2)\y=128+cn(ang2)*radii2

		ang2.w=ang-5 : If ang2<0 Then ang2+360
		polygon(3)\x=160+sn(ang2)*radii2
		polygon(3)\y=128+cn(ang2)*radii2

		Polyf 3,&polygon(1),col
	Until angg=endang
End Statement

.StartUp:
AMIGA
For h.l=0 To 5
	BitMap 0,320,256,5
Next h

;
; simon was here, butchered to save disk space...
;
 Print "please wait..."
 Use BitMap 0
 For r=160 To 1 Step -1
	 c=r MOD 55:c * 30/55:If c>16 Then c=32-c
	Circlef 160,128,r,c
 Next
;
;
;

For h.l=1 To 5
	ShapesBitMap 0,h
Next h

For h=0 To 359
	a=(h*Pi)/180
	sn(h)=HSin(a)
	cn(h)=HCos(a)
Next h

Use BitMap 0
BitMapOutput 0

For h=0 To 15
	StopCycle 0,h,h,h,0
	StopCycle 0,31-h,15-h,15-h,15-h
Next h

For h=0 To 5
	Free BitMap 9
	BitPlanesBitMap h,9,%10000
	Use BitMap 9
	DrawTriangles {0+h+h,200,128,1}
	DrawTriangles {360-h-h,127,80,1}
	DrawTriangles {0+h+h,79,40,1}
	DrawTriangles {360-h-h,39,10,1}
Next h

BLITZ
InitCopList 0,44,256,$5,8,32,0
DisplayBitMap 0,0
DisplayPalette 0,0
CreateDisplay 0

dbuffer=1
Repeat
	dbuffer+1 : If dbuffer>5 Then dbuffer=1
	DisplayBitMap 0,dbuffer
	VWait 2
Until Joyb(0)
End
