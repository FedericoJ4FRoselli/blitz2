; ascii translation of /Users/simon/Google Drive/amiga/w/lha/bbold/cricket/bup/ui2.bb,/Users/simon/Google Drive/amiga/w/lha/bbold/cricket/bup2/ui2.bb
;
; cricket scoring program
;

; (C)1994 Armstrong Communications Ltd

; special cases
;
; partner can get run out
; narrator to be enabled on OK
; 2 runs on a noball???
; save events so reset can power up correctly
;
; batter order
; chasing figure
; over needs updating in stats before start of next
;

tab$=Chr$(9)

NEWTYPE .batter
	ballsfaced.w
	runs.w
	fours.w
	sixes.w
	minutes.w
	status.w
	bowler.w
	fielder.w
End NEWTYPE

NEWTYPE .bowler
	overs.w					 ;bowling
	maidens.w
	runsallowed.w
	wickets.w
	extras.w
	balls.w
	noballs.w
	byes.w
	legbyes.w
	wides.w
End NEWTYPE

NEWTYPE .side
	teamname$
	teamabbrev$
	name$[12]
	runs.w
	extras.w
	wickets.w
	;
	cscore.w
	cwickets.w
	;
	ba.w[12]				;batting order
	bo.w[12]				;bowling order
	bat.batter[12]
	bowl.bowler[12]
End NEWTYPE

NEWTYPE .entry
	message$			 ;displayed in view window
	id.w					 ;0=empty 1=newinnings 2=newbatsmen 3=over 4=ball
	batting.w			;1 innings team batting
	batter.w			 ;2 define batsmen
	partner.w
	bowler.w			 ;3 over
	whichend.w
	over.w
	ball.w
	shot.w				 ;4 ball
	runs.w
	extras.w
	wx.w
	wy.w
	batterout.w
	fielder.w
	newbatter.w
End NEWTYPE

Dim team.side(1)
Dim entry.entry(1023)
Dim ends$(2)
Dim shot$(20)
Dim out$(20)
Dim inst$(5)

ends$(0)="NEAR END":ends$(1)="FAR END"

shot$(0)="* IS FACING (& OPPOSITE)"
shot$(1)="* MISSES"
shot$(2)="* HITS"
shot$(3)="NOBALL"
shot$(4)="WIDE"
shot$(5)="BYE"
shot$(6)="LEGBYE"
shot$(7)="* HITS A FOUR!"
shot$(8)="* HITS A SIX!"
shot$(10)="* IS CAUGHT BY ?"
shot$(11)="* IS BOWLED"
shot$(12)="* IS STUMPED BY ?"
shot$(13)="* IS RUNOUT"
shot$(14)="& IS RUNOUT"
shot$(15)="* RETIRES"
shot$(16)="* HITWICKET AND IS OUT"
shot$(17)="* HANDLEDBALL AND IS OUT"
shot$(18)="* IS OUT BY LBW"

inst$(0)="													 SELECT BEGIN												 "
inst$(1)="					 SELECT TEAM FROM BELOW LEFT TO BAT AND SELECT OK		 "
inst$(2)="								SELECT BATSMEN FROM BELOW AND SELECT OK				 "
inst$(3)="							SELECT BOWLER AND TOGGLE ENDS FOR NEXT OVER			 "
inst$(4)="							 SELECT FUNCTION FROM RIGHT THEN SELECT OK				"
inst$(5)="												SELECT NEW BATSMAN											"

Function.s nth{n.w}
	a$=Str$(n):If n>20 Then n=n MOD 10
	Select n
		Case 1:a$+"st"
		Case 2:a$+"nd"
		Case 3:a$+"rd"
		Default:a$+"th"
	End Select
	Function Return a$
End Function

.initialise
	For i=0 To 11:For j=0 To 1:team(j)\ba[i]=i:team(j)\bo[i]=i:Next:Next
	batting=0
	Gosub readteam
	Gosub setuppalette
	Gosub setupscreen
	Gosub drawoval
	Gosub initevents
	Gosub printevents
	;
	Gosub dostats
	Gosub printstats

.main
	;
	Repeat
		;
		Gosub displayevent
		ev.l=WaitEvent
		id=-1:If ev=$20 Then id=GadgetHit
		;
		If id=200 Then End
		;
		USEPATH entry(cmess)
		;
		If id=105 Then \id=0:nxmess-1:cmess-1	 ;backup!
		;
		If id=104															;end of innings
			\id=0:\message="END OF INNINGS"
			\batting=1-\batting
			Gosub initnextevent
			\id=1
		EndIf
		;
		Select \id
			Case 0:
				If id=101 Then cmess=nxmess:nxmess+1:\id=1
				;
			Case 1:
				If id=0 Then \batting=0
				If id=20 Then \batting=1
				If id=69 Then Gosub tobatok
				;
			Case 2:
				If id>0 AND id<13 Then Gosub openingbatter
				If id=69 Then Gosub openingbattersok
				;
			Case 3:
				If id>20 AND id<33 Then \bowler=team(1-\batting)\bo[id-21]
				If id=58 Then \whichend=1-\whichend
				If id=69 Then bowl=\bowler:Gosub bowlerok
				;
			Case 4:
				If id=59 Then Exchange \batter,\partner
				If id>49 AND id<58 Then \shot=id-49:Activate 1	 ;1..8
				If id>59 AND id<69
					\shot=id-50							;10..17
					If \shot=14 Then \batterout=\partner Else \batterout=\batter
				EndIf
				If id>20 AND id<33
					If \shot=10 OR \shot=12 OR \shot=13 OR \shot=14
						\fielder=team(1-\batting)\bo[id-21]
					EndIf
				EndIf
				If ev=$8 AND EventWindow=1 Then Gosub ovalclick
				If id=69 Then Gosub ballok
				;
			Case 5:
				If id>0 AND id<13 Then Gosub newbatter
				If id=69 AND \newbatter>-1 Then Gosub newbatterok
				;
		End Select
		;
	Forever

.tobatok:
	USEPATH entry(cmess)
	Gosub listplayers
	Gosub initnextevent:\id=2:\batter=1:\partner=0
	Return

.newbatter:
	USEPATH entry(cmess)
	bat=team(batting)\ba[id-1]
	If team(batting)\bat[bat]\status<>0 Then BeepScreen 0:Return
	\newbatter=bat
	Return

.newbatterok:
	USEPATH entry(cmess)
	If \batter=\batterout Then \batter=\newbatter Else \partner=\newbatter
	Gosub sortbatters
	Gosub initnextevent
	\ball+1:If \ball=7 Then \id=3:\ball=0:\whichend=1-\whichend Else \id=4
	Return

.openingbattersok:
	USEPATH entry(cmess)
	If \batter=\partner Then BeepScreen 0:Return
	Gosub sortbatters
	Gosub initnextevent:\id=3:\over=1
	Return

.bowlerok:
	USEPATH team(1-batting)
	i=-1:Repeat:i+1:Until \bo[i]=bowl:j=i
	If i>0
		Repeat
			i-1
			If \bowl[\bo[i]]\overs=0 AND \bowl[\bo[i]]\balls=0
				Exchange \bo[i],\bo[j]:j=i
			Else
				i=0
			EndIf
		Until i=0
	EndIf
	USEPATH entry(cmess)
	Gosub listplayers
	Gosub initnextevent
	\id=4:\ball=1:\over+1
	Exchange \batter,\partner
	Return

.ballok:
	USEPATH entry(cmess)
	If \shot=0 Then BeepScreen 0:Return								 ;notplayed shot
	If \shot=10 OR \shot=12 OR \shot=13 OR \shot=14		 ;no fielder entered
		If \fielder=-1 Then BeepScreen 0:Return
	EndIf
	batterout=\batterout:shot=\shot:odd=(\runs OR \extras) AND 1
	Gosub initnextevent
	If shot<10
		If shot<>3 AND shot<>4 Then \ball+1
		If odd Then Exchange \batter,\partner
		If \ball=7 Then \id=3:\ball=0:\whichend=1-\whichend Else \id=4
	Else
		\id=5:\newbatter=-1:\batterout=batterout
	EndIf
	Return

.initnextevent
	USEPATH entry(cmess)
	;
	currover=\over
	Gosub dostats
	Gosub printstats
	Gosub makescoreticks
	Gosub makefiles
	;
	*nx.entry=entry(cmess+1)
	If *nx\id=0
		*nx\batting=\batting,\batter,\partner,\bowler,\whichend,\over,\ball
		*nx\shot=0,0,0,0,0,0,0
		*nx\fielder=-1
		runs=0
	EndIf
	cmess=nxmess:nxmess+1
	Return

.displayevent:
	USEPATH entry(cmess)
	WLocate 6,99:Print inst$(\id)
	Select \id
		Case 0
			\message="WAITING TO COMMENCE PLAY"
		Case 1
			\message="TEAM TO BAT IS "+team(\batting)\teamname
		Case 2
			t$=team(\batting)\name[\batter]+" AND "
			t$+team(\batting)\name[\partner]
			\message="NEW BATSMEN ARE "+t$
		Case 3
			t$="OVER#"+Str$(\over)+":"
			t$+team(1-\batting)\name[\bowler]
			\message=t$+" IS BOWLING FROM "+ends$(\whichend)
			Gosub drawoval
		Case 4
			t$=team(\batting)\name[\batter]
			u$=team(\batting)\name[\partner]
			;
			g$=" BALL#"+Str$(\ball)+":"
			g$+shot$(\shot)
			g$=Replace$(g$,"*",t$)
			g$=Replace$(g$,"&",u$)
			;
			If \fielder>-1
				t$=team(1-\batting)\name[\fielder]
				g$=Replace$(g$,"?",t$)
			EndIf
			;
			\runs=0:\extras=0
			Select \shot
				Case 2:\runs=runs:\extras=0
				Case 3:\runs=0:If runs<3 Then \extras=2 Else \runs=runs ;noball
				Case 4:\runs=0:\extras=1																;wide
				Case 5:\runs=0:\extras=runs
				Case 6:\runs=0:\extras=runs
				Case 7:runs=4:\runs=4:\extras=0
				Case 8:runs=6:\runs=6:\extras=0
			End Select
			;
			If \runs Then g$+" "+Str$(\runs)+" RUNS"
			If \extras Then g$+" "+Str$(\extras)+" EXTRAS"
			;
			\message=g$
			;
			Gosub updateoval
		Case 5
			t$="?"
			If \newbatter>-1Then t$=team(\batting)\name[\newbatter]
			\message="	NEW BATSMAN IS "+t$
	End Select
	Gosub printevents
	Return

.openingbatter:
	USEPATH entry(cmess)
	bat=id-1
	If bat=\batter OR bat=\partner
		Exchange \batter,\partner
	Else
		\batter=\partner:\partner=bat
	EndIf
	Return

.ovalclick:
	USEPATH entry(cmess)
	If EMouseX<0 OR EMouseX>191 OR EMouseY<0 OR EMouseY>109 Then Return
	Use Window 1
	x=QLimit(EMouseX-4,0,184):y=QLimit(EMouseY-4,0,102)
	If \whichend=0
		ex=94:ey=34:\wx=x-ex:\wy=y-ey
	Else
		ex=94:ey=34+21:\wx=x-ex:\wy=ey-y
	EndIf
	If \shot>1 AND \shot<7
		mb=MButtons
		If mb=1 Then runs=QLimit(runs+1,0,9)
		If mb=2 Then runs=QLimit(runs-1,0,9)
	EndIf
	If \shot<2 Then \shot=2
	Gosub updateoval
	Activate 1
	Use Window 0
	Return

.updateoval:
	USEPATH entry(cmess)
	ShapesBitMap 0,1
	If \shot>1 AND \shot<9
		If \whichend=0
			ex=94:ey=34:x=ex+\wx:y=ey+\wy
		Else
			ex=94:ey=34+21:x=ex+\wx:y=ey-\wy
		EndIf
		Use BitMap 1:BitMapOutput 1
		Line ex,ey,x,y,1:Locate x/8,y/8:Print runs
	EndIf
	BitMaptoWindow 1,1
	WindowOutput 0
	Return

.drawoval:
	ends=entry(cmess)\whichend
	Use BitMap 0:BlockScroll 3
	Circlef 95,46,80,45,4
	Boxf 95-10,46-12,95+10,46+12,5
	ex=94:ey=34+ends*21
	For x=-1 To 1:Boxf ex+x*6,ey,ex+2+x*6,ey+1,1:Next
	ShapesBitMap 0,1:BitMaptoWindow 1,1
	Return

.sortbatters:
	USEPATH entry(cmess)
	ba=\partner:Gosub movehim
	ba=\batter:Gosub movehim
	Gosub listplayers
	Return

movehim:
	USEPATH team(batting)
	i=-1:Repeat:i+1:Until \ba[i]=ba:j=i
	If i>0
		Repeat
			i-1
			If \bat[\ba[i]]\ballsfaced=0 Then Exchange \ba[i],\ba[j]:j=i Else i=0
		Until i=0
	EndIf
	Return

.listplayers
	USEPATH team(batting)
	batting=entry(cmess)\batting:bowling=1-batting
	For i=0 To 11
		##[$C622] 0,i+1,LSet$(\name[\ba[i]],12):Redraw 0,i+1
	Next
	USEPATH team(bowling)
	For i=0 To 11
		##[$C622] 0,i+21,LSet$(\name[\bo[i]],12):Redraw 0,i+21
	Next
	Return

.setupscreen
	Screen 0,0,0,736,283,3,$8000,"HELLO",1,2:FadePalette 0
	USEPATH team(x)
	;
	For x=0 To 1
		t$="("+LSet$(\teamabbrev,2)+")"
		TextGadget 0,6+x*56,150,0,x*20,t$
		For i=0 To 11
			TextGadget 0,x*262+120,i*12+137,0,x*20+i+1,LSet$(\name[i],12)
		Next
	Next
	;
	Restore funcs:id=50
	For x=0 To 1
		For i=0 To 9
			Read a$:a$=LSet$(a$,7):m=0;:If id>52 AND id<60 Then m=1
			TextGadget 0,598+x*68,3+i*12,m,id,a$:id+1
		Next
	Next
	;
	PropGadget 0,0,0,$80,100,12,96
	TextGadget 0,6,130,0,101,"	 BEGIN	 "
	TextGadget 0,6,190,0,102,"STOP"
	TextGadget 0,54,190,0,103,"START"
	TextGadget 0,6,230,0,104,"END INNINGS"
	TextGadget 0,6,250,0,105,"	BACK UP! "
	TextGadget 0,6,268,0,200,"	 QUIT		"
	;
	Window 0,0,0,736,283,$1800,"",1,2,0:MenusOff
	;
	ResetString 18,2,370,92
	ResetString 4,99,584,7
	ResetString 398,2,189,92
	ResetString 598,2,132,122
	ResetString 4,129,102,152
	ResetString 118,129,250,152
	ResetString 380,129,350,152
	;
	ResetString 10,176,92,10
	WLocate 28,177:Print "0:00:00"
	;
	WLocate 138,129:Print "BATTERS"
	WLocate 414,129:Print "BOWLERS"
	;
	Window 1,397,1,192,94,$800,"",1,2:MenusOff
	BitMap 0,192,94,3:BitMap 1,192,94,3
	;
	Return

.funcs:
	Data.s MISS,HITS,NOBALL,WIDE,BYE,LEGBYE,FOUR,SIX,SWAPEND,SWAPBAT
	Data.s CAUGHT,BOWLED,STUMPED,BRUNOUT,PRUNOUT,RETIRED,HITWICKET,HANDLEDBALL,LBW,OK

.setuppalette
	StopCycle 0,0,8,8,8:StopCycle 0,1,15,15,15:StopCycle 0,2,0,0,0:StopCycle 0,3,4,8,12
	StopCycle 0,4,4,12,4:StopCycle 0,5,0,8,0:StopCycle 0,6,8,4,12:StopCycle 0,7,15,15,8
	Return

.readteam
	If WriteMem(0,"teams")
		FileInput 0
		USEPATH team(i)
		For i=0 To 1
			\teamname=Edit$(80)
			\teamabbrev=Edit$(80)
			For j=0 To 11:\name[j]=Edit$(80):Next
		Next
		CloseFile 0
	Else
		NPrint "Couldn't Open Team File!":End
	EndIf
	Return

.initevents:
	Restore header
	For i=0 To 7:Read entry(i)\message:Next
	nxmess=9:cmess=8:Return

.printevents
	Use Window 0:WindowOutput 0
	moff=QLimit(cmess-9,0,4096)
	For i=0 To 9
		ii=moff+i:If ii=cmess Then col=1 Else col=7
		WColour col:WLocate 20,2+i*9:Print LSet$(entry(ii)\message,46)
	Next
	WColour 1,0
	Return

.header						;46 chars wide
	Data.s "			 SKY CRICKET STATISTICS SOFTWARE				"
	Data.s "																							"
	Data.s "							V1.0 24/11/94									 "
	Data.s "																							"
	Data.s "		(C)1994 ARMSTRONG COMMUNICATIONS LTD			"
	Data.s "																							"
	Data.s "					 MAY THE BEST TEAM WIN							"
	Data.s "																							"

;---------------------------------------------------------------------

.printstats
	;
	Use Window 0:WindowOutput 0
	;
	USEPATH team(batting)\bat[j]
	WColour 1:WLocate 230,129:Print " B	 R	4	6	 M "
	For i=0 To 11
		j=team(batting)\ba[i]
		If \ballsfaced>0
			t$=Centre$(Str$(\ballsfaced),3)+" "
			t$+Centre$(Str$(\runs),3)+" "
			t$+Centre$(Str$(\fours),2)+" "
			t$+Centre$(Str$(\sixes),2)+" "
			t$+Centre$(Str$(\minutes),3)
		Else
			t$=String$(" ",17)
		EndIf
		WColour 2:WLocate 230,139+i*12:Print t$
	Next
	;
	USEPATH team(batting)
	t$=\teamname+"	("+\teamabbrev+") "
	t$+Str$(\runs+\extras)+"-"+Str$(\wickets)+"	"
	u$=nth{currover}
	t$+u$+" OVER				"
	;
	WColour 2:WLocate 4,109:Print t$
	;
	USEPATH team(1-batting)\bowl[j]
	WColour 1:WLocate 492,129:Print " O B	M	R	W	X nb	b lb	w "
	For i=0 To 11
		j=team(1-batting)\bo[i]
		If \overs<>0 OR \balls<>0
			t$=Centre$(Str$(\overs),2)+" "
			t$+Centre$(Str$(\balls),2)+" "
			t$+Centre$(Str$(\maidens),1)+" "
			t$+Centre$(Str$(\runsallowed),3)+" "
			t$+Centre$(Str$(\wickets),2)+" "
			t$+Centre$(Str$(\extras),2)+" "
			t$+Centre$(Str$(\noballs),2)+" "
			t$+Centre$(Str$(\byes),2)+" "
			t$+Centre$(Str$(\legbyes),2)+" "
			t$+Centre$(Str$(\wides),2)+" "
		Else
			t$=String$(" ",29)
		EndIf
		WColour 2:WLocate 492,139+i*12:Print t$
	Next
	;
	USEPATH team(1-batting)
	t$=\teamname+"	("+\teamabbrev+") "
	t$+Str$(\runs+\extras)+"-"+Str$(\wickets)+"	("
	t$+Str$(\cscore)+"-"+Str$(\cwickets)+")				"
	;
	WColour 2:WLocate 4,118:Print t$
	WColour 1
	Return

.dostats:							 ;set currover for
	;
	USEPATH entry(i)
	;
	For i=0 To 1
		team(i)\runs=0
		team(i)\extras=0
		team(i)\wickets=0
		For j=0 To 11
			team(i)\bat[j]\ballsfaced=0,0,0,0,0,0,0,0
			team(i)\bowl[j]\overs=0,0,0,0,0,0,0,0,0,0
		Next
	Next
	;
	maidin=False:*bo.bowler=0
	;
	For i=0 To 1023
		;
		If *bo<>0 AND (\id=3 OR \id=1)
			*bo\balls=0
			*bo\overs+1
			If maidin Then *bo\maidens+1
			*bo=0
		EndIf
		;
		Select \id
			;
			Case 1:																		 ;innings
				*b.side=team(\batting)
				*f.side=team(1-\batting)
			Case 3:																		 ;over
				If \over=currover
					*b\cscore=*b\runs+*b\extras
					*b\cwickets=*b\wickets
				EndIf
				maidin=1
				*bo=*f\bowl[\bowler]
			Case 4:
				*ba.batter=*b\bat[\batter]
				If \shot>0 AND \shot<10
					*ba\ballsfaced+1
					*ba\runs+\runs
					If \shot=7 Then *ba\fours+1
					If \shot=8 Then *ba\sixes+1
					*b\runs+\runs
					*b\extras+\extras
					*bo\runsallowed+\runs
					*bo\extras+\extras
					If \shot=3 Then *bo\noballs+1
					If \shot=4 Then *bo\wides+1
					If \shot=5 Then *bo\byes+1
					If \shot=6 Then *bo\legbyes+1
					If \runs>0 OR \extras>0 Then maidin=0
				EndIf
				If \shot>9
					*ba\status=\shot
					*ba\bowler=\bowler
					*ba\fielder=\fielder
					If \shot<>15 Then *b\wickets+1:*bo\wickets+1	 ;not retired
				EndIf
				*bo\balls+1
		End Select
	Next
	Return

;ScoreTick1.TXT
;<<ND 225-9>>
;<<49th Over>>

;ScoreTick2.TXT
;<<ND 225-9>>
;<<49th Over>>
;<<CD 270-7>>

.makescoreticks:
	USEPATH team(batting)
	t1$=\teamabbrev+" "+Str$(\runs+\extras)+"-"+Str$(\wickets)
	t2$=nth{currover}:t2$+" Over"
	USEPATH team(1-batting)
	t3$=\teamabbrev+" "+Str$(\cscore)+"-"+Str$(\cwickets)
	If ReadMem(0,"scoretick1.txt")
		FileOutput 0
		NPrint t1$+Chr$(13):NPrint t2$+Chr$(13)
		CloseFile 0
	EndIf
	If ReadMem(0,"scoretick2.txt")
		FileOutput 0
		NPrint t1$+Chr$(13):NPrint t2$+Chr$(13):NPrint t3$+Chr$(13)
		CloseFile 0
	EndIf
	WindowOutput 0
	Return

;AllBowlersBL.TXT
;BatsmanInBL.TXT
;BatsmanOutBL.TXT
;Batting Scorecard.TXT
;BowlerBL.TXT
;BowlingScorecard.TXT
;ComparisonBL.TXT
;Equation+BL.TXT
;EquationBL.TXT
;PartnershipBL.TXT
;StandardScoreBL.TXT

.makefiles:
	;
	;AllBowlersBL.TXT
	;
	;<<MORRISON>>
	;<t><<^^3<t>2<t>^26<t>1>>
	;
	USEPATH team(1-batting)\bowl[j]
	;
	If ReadMem(0,"allbowlersbl.txt")
		FileOutput 0
		For i=0 To 11
			j=team(1-batting)\bo[i]
			If \overs<>0 OR \balls<>0
				NPrint team(1-batting)\name[j]+Chr$(13)
				t$=Chr$(9)
				If \balls=0 Then t$+RSet$(Str$(\overs),3) Else t$+Str$(\overs)+"."+Str$(\balls)
				t$+Chr$(9)+Str$(\maidens)+Chr$(9)+RSet$(Str$(\runsallowed),3)+Chr$(9)+Str$(\wickets)
				NPrint t$+Chr$(13)
			EndIf
		Next
		CloseFile 0
	EndIf
	;
	;BatsmanInBL.TXT
	;
	;<<McSWEENEY>>
	;<<88>>
	;<<3 FOURS	 2 SIXES	 222 Mins	 122 BALLS>>
	;
	USEPATH team(batting)\bat[j]
	;
	If ReadMem(0,"batsmaninbl.txt")
		FileOutput 0
		j=entry(cmess)\batter
		NPrint team(batting)\name[j]+Chr$(13)
		NPrint Str$(\runs)+Chr$(13)
		t$=Str$(\fours)+" FOURS"+Chr$(9)
		t$+Str$(\sixes)+" SIXES"+Chr$(9)
		t$+Str$(\minutes)+" Mins"+Chr$(9)
		t$+Str$(\ballsfaced)+" BALLS"
		NPrint t$+Chr$(13)
		CloseFile 0
	EndIf
	;
	;BatsmanOutBL.TXT
	;
	;<<McSWEENEY>>
	;<<88>>
	;<t><<ST Robertson	b Hartshorne>>
	;<<3 FOURS	 2 SIXES	 222 Mins	 122 BALLS>>
	;<<225-9>>	--- Pad score here as ^5-9^, then ^55-9, then 225-9
	;
	USEPATH team(batting)\bat[j]
	*f.side=team(1-batting)
	;
	If ReadMem(0,"batsmanoutbl.txt")
		FileOutput 0
		j=entry(cmess)\batterout
		NPrint team(batting)\name[j]+Chr$(13)
		NPrint Str$(\runs)+Chr$(13)
		Gosub outstatusstring
		t$=Chr$(9)
		Gosub outstatusstring
		NPrint t$+Chr$(13)
		t$=Str$(\fours)+" FOURS"+Chr$(9)
		t$+Str$(\sixes)+" SIXES"+Chr$(9)
		t$+Str$(\minutes)+" Mins"+Chr$(9)
		t$+Str$(\ballsfaced)+" BALLS"
		NPrint t$+Chr$(13)
		NPrint score$
		CloseFile 0
	EndIf
	;
	;Batting Scorecard.TXT
	;
	;<<NORTHERN DISTRICTS>>
	;<<BATSMAN1>>
	; |||||||||
	;<<BATSMAN11>>
	;
	;<t><t>NOT Out<t><t><t>^88<t>^(64)
	;<t><t><t>b
	;<t>c<t><t>b
	;<t>ST<t><t>b
	;<t><t>Lbw<t>b
	;<t><t>NOT Out<t><t><t>^44<t>^(44)
	;<t><t><t><t>Kuggeleijn<t>^10<t>^(10)
	;<t><t>Kuggeleijn<t><t>Wilson<t>^^0<t>^^(1)
	;<t><t>Wilson<t><t>Walker<t>^22<t>^(14)
	;<t><t><t><t>Mallendar<t>^^8<t>^^(7)
	;
	;49.5 Overs
	;<t><t>TOTAL<t><t>9 wkts		225
	;<<CurrentBATSMAN1>>
	;<<CurrentBATSMAN2>>
	;
	USEPATH team(batting)\bat[j]
	*f.side=team(1-batting)
	;
	If ReadMem(0,"batting scorecard.txt")
		FileOutput 0
		NPrint team(batting)\teamname+Chr$(13)
		For i=0 To 10
			j=team(batting)\ba[i]:NPrint team(batting)\name[j]+Chr$(13)
		Next
		For i=0 To 10
			j=team(batting)\ba[i]
			If \status>9
				Gosub outstatusstring
				t$+Chr$(9)+RSet$(Str$(\runs),3)+Chr$(9)+"("+Str$(\ballsfaced)+")"
			Else
				If j=entry(cmess)\batter OR j=entry(cmess)\partner
					t$=tab$+tab$+"Not Out"+tab$+tab$+tab$
					t$+RSet$(Str$(\runs),3)+Chr$(9)+"("+Str$(\ballsfaced)+")"
				Else
					t$="p	p"
				EndIf
			EndIf
			NPrint t$+Chr$(13)
		Next
		;
		t$="nn.n Overs":NPrint t$+Chr$(13)
		t$=tab$+tab$+score$+tab$+tab$+wickets$+" wkts"
		;
		j=entry(cmess)\batter:NPrint team(batting)\name[j]+Chr$(13)
		j=entry(cmess)\partner:NPrint team(batting)\name[j]+Chr$(13)
		;
		CloseFile 0
	EndIf
	;
	;BowlerBL.TXT
	;
	;<<MORRISON>>
	;<t><<^^3<t>2<t>^26<t>1>>
	;
	USEPATH team(1-batting)\bowl[j]
	;
	If ReadMem(0,"bowlerbl.txt")
		FileOutput 0
		j=entry(cmess)\bowler
		NPrint team(1-batting)\name[j]+Chr$(13)
		t$=Chr$(9)
		If \balls=0 Then t$+RSet$(Str$(\overs),3) Else t$+Str$(\overs)+"."+Str$(\balls)
		t$+Chr$(9)+Str$(\maidens)+Chr$(9)+RSet$(Str$(\runsallowed),3)+Chr$(9)+Str$(\wickets)
		NPrint t$+Chr$(13)
		CloseFile 0
	EndIf
	;
	;BowlingScorecard.TXT
	;
	;<<NORTHERN DISTRICTS>>
	;<<BOWLER1>>
	;<<BOWLER2>>
	;<<BOWLER3>>
	;<<BOLWER4>>
	;<<BOWLER5>>
	;<<BOWLER6>>
	;<<BOWLER7>>
	;<t>^5<t>2<t>12<t><t>-<t>1
	;<t><t><t><t>0
	;<t>10<t>0<t>68<t><t>2<t>-
	;<t><t><t><t>2
	;<t>9.5<t>1<t>49<t><t>-<t>-
	;<t><t><t><t>1
	;<t>10<t>0<t>56<t><t>1<t>-
	;<t><t><t><t>2
	;<t>^5<t>0<t>30<t><t>3<t>2
	;<t><t><t><t>1
	;<t>10<t>0<t>44<t><t>-<t>2
	;<t><t><t><t>3
	;<t>77<t>7<t>77<t><t>7<t>7
	;<t><t><t><t>7
	;^24
	;<t>^0<t><t>12<t><t>7<t>5
	;<t>107^^123^^177
	;
	;
	WindowOutput 0
	Return

outstatusstring:
	USEPATH team(batting)\bat[j]
	*f.side=team(1-batting)
	t$=Chr$(9)
	If \status=10 AND \fielder>-1 Then t$+"c "+*f\name[\fielder]+"	"
	If \status=12 AND \fielder>-1 Then t$+"st "+*f\name[\fielder]+"	"
	If \status<13 Then t$+"b "+*f\name[\bowler]
	If \status=13 OR \status=14 Then t$+"Run Out"
	If \status=15 Then t$+"Not Out"
	If \status=16 Then t$+"Hit Wicket"
	If \status=17 Then t$+"Handled Ball"
	If \status=18 Then t$+"lbw "+*f\name[\bowler]
	Return