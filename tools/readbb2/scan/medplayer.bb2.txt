; ascii translation of /Users/simon/Google Drive/amiga/w/disks/bums/bum7/disk2/gtb/Demos/MEDPlayer.bb2,/Users/simon/Google Drive/amiga/w/lha/bbold/disks/bums/bum7/disk2/gtb/Demos/MEDPlayer.bb2
; []----------------------------------------------------------------------[]
; |							 (C) 1994 Tim Caldwell. All rights reserved.							|
; |																																				|
; |					File: MEDPlayer.bb2																					 |
; |				Author: Tim Caldwell																						|
; | Modifications: Sat 21.05.94	CREATED																	 |
; |								Interface created using GTB. This program took just		 |
; |								under 20 mins to write and test. The window and menus	 |
; |								took 5 minutes: GTB created the source and basic event	|
; |								structure.																							|
; []----------------------------------------------------------------------[]

WBStartup

DEFTYPE .s FileName,FilePath,FullName
DEFTYPE .w ModuleLoaded,UseRequester,MEDVolume
DEFTYPE .w

MaxLen FileName = 192
MaxLen FilePath = 192


main:
	FilePath	= "SYS:"
	MEDVolume = 64

	LoadFont 0,"GTBGadget.font",8

	MenuGap		2,1
	SubItemOff 100,0
	MenuTitle	0,0,"File"
	MenuItem	 0,$00,0,0,LSet$("Open...",14),"O"
	MenuItem	 0,$40,0,1,String$(Chr$(127),14)
	MenuItem	 0,$00,0,2,"Minimise"
	MenuItem	 0,$00,0,3,"Maximise"
	MenuItem	 0,$40,0,4,String$(Chr$(127),14)
	MenuItem	 0,$00,0,5,"Quit","Q"
	MenuTitle	0,1,"Player"
	MenuItem	 0,$00,1,0,LSet$("Stop Module",17),"S"
	MenuItem	 0,$00,1,1,"Play Module","P"
	MenuItem	 0,$00,1,2,"Cont Module"

	FindScreen		0
	DefaultIDCMP	$00000374
	Window				0,88,43,320,77,$0020100E,"MED Player",1,2
	SetMenu			 0
	GTSlider			0,0,8,4,296,14,"",$0021,1,64,MEDVolume
	GTButton			0,1,8,18,68,14,"File...",$0010
	GTString			0,2,76,18,228,14,"",$0001,80,""
	GTButton			0,3,8,46,68,14,"Play",$0010
	GTButton			0,4,76,46,68,14,"Cont",$0010
	GTButton			0,5,144,46,68,14,"Stop",$0010
	GTButton			0,6,236,46,68,14,"Quit",$0010
	AttachGTList	0,0

	CloseWin = False
	Repeat
		EventType.l = WaitEvent
		Select EventType
			Case $00000200
				CloseWin = True

			Case $00000100
				Select MenuHit
					Case 0
						Select ItemHit
							Case 0 : UseRequester = True
											 Gosub OpenModule
							Case 2 : WSize 320,11
							Case 3 : WSize 320,77
							Case 5 : CloseWin = True
						End Select
					Case 1
						Select ItemHit
							Case 0 : Gosub StopPlayer
							Case 1 : Gosub StartPlayer
							Case 2 : Gosub Continue
						End Select
				End Select

			Case $00000040
				Select GadgetHit
					Case 0 : MEDVolume = EventCode
									 If ModuleLoaded Then SetMedVolume MEDVolume
					Case 1 : UseRequester = True
									 Gosub OpenModule
					Case 2 : FullName = GTGetString(0,2)
									 UseRequester = False
									 Gosub OpenModule
					Case 3 : Gosub StartPlayer
					Case 4 : Gosub Continue
					Case 5 : Gosub StopPlayer
					Case 6 : CloseWin = True
				End Select
		End Select
	Until CloseWin
	CloseWindow 0
	CloseScreen 0
	Free GTList	 0
	Free ##[$C40D] 0
	End


; ** OpenModule
; **
OpenModule:
	If UseRequester
		FullName = ASLFileRequest$ ("Open MED Module"+Chr$(0),FilePath,FileName,"#?.med")
	EndIf
	If FullName
		If ModuleLoaded
			Gosub StopPlayer
			ModuleLoaded = False
		EndIf
		SetErr
			GTSetString 0,2,"Not a MED module !"
			Return
		End SetErr
		LoadMedModule 0,FullName
		GTSetString 0,2,FullName
		ModuleLoaded = True
		Gosub StartPlayer
	EndIf
	ClrErr
	Return


; ** StartPlayer
; **
StartPlayer:
	If ModuleLoaded = False Then Return
	Gosub StopPlayer
	StartMedModule 0
	SetMedVolume MEDVolume
	Gosub VBlankInt
	Return


; ** Continue
; **
Continue:
	If ModuleLoaded = False Then Return
	Gosub StopPlayer
	SetMedVolume MEDVolume
	Gosub VBlankInt
	Return


; ** StopPlayer
; **
StopPlayer:
	If ModuleLoaded
		ClrInt 5
		SetMedVolume 0
		Volume $F,0,0,0,0
	EndIf
	Return


; ** VBlankInt
; **
VBlankInt
	SetInt 5
		PlayMed
	End SetInt
	Return
